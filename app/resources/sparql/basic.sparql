PREFIX premis: <http://www.loc.gov/premis/v3#>
PREFIX premisowl: <http://www.loc.gov/premis/rdf/v3/>
PREFIX xsi: <http://www.w3.org/2001/XMLSchema-instance#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX haObjId: <https://data.hetarchief.be/id/object/>
PREFIX relSubType: <http://id.loc.gov/vocabulary/preservation/relationshipSubType/>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>
PREFIX cryptographicHashFunctions: <http://id.loc.gov/vocabulary/preservation/cryptographicHashFunctions/>
PREFIX edtf: <http://id.loc.gov/datatypes/edtf/>
PREFIX schema: <https://schema.org/>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>

CONSTRUCT
  {
    ?ie a premisowl:IntellectualEntity;
        premisowl:identifier ?local_id;
        relSubType:isr ?rep;
        ?dcterm_p ?dcterm_o .

    ?local_id  a ?local_id_type_uri; 
         rdf:value ?local_id_val .

    ?f_uri a premisowl:fixity;
        rdf:value ?file_digest.

    ?rep a premisowl:Representation;
        relSubType:rep ?ie;
        relSubType:inc ?file .

     ?file a premisowl:File;
        relSubType:isi ?rep;
        premisowl:originalName ?file_original_name;
        premisowl:format ?file_format;
        premisowl:size ?file_size;
        premisowl:fixity ?f_uri.
  }

WHERE
  {
    SERVICE <x-sparql-anything:location={{ bag_path }}/data/metadata/preservation/premis.xml> {
        {
            {
                SELECT DISTINCT ?local_id_type ?local_id_val
                WHERE {
                    [] a premis:object;
                    xsi:type "premis:intellectualEntity";
                    ?lid_1 [
                        a premis:objectIdentifier;
                        ?lid_2 [
                            a premis:objectIdentifierType;
                                rdf:_1 ?local_id_type
                        ];
                        ?lid_3 [
                            a premis:objectIdentifierValue;
                                rdf:_1 ?local_id_val
                        ]
                    ]
                    FILTER (?local_id_type != "UUID")
                }
            }
            BIND(iri(concat(str(haObjId:), ?local_id_type)) AS ?local_id_type_uri)
            BIND(iri(concat(str(haObjId:), ?local_id_val)) AS ?local_id)
        }

        [] a premis:object;
            xsi:type "premis:intellectualEntity";
            ?ie_1 [
                a premis:objectIdentifier;
                    ?ie_2 [
                            a premis:objectIdentifierType;
                                rdf:_1 ?ie_type
                        ];
                        ?ie_3 [
                            a premis:objectIdentifierValue;
                                rdf:_1 ?ie_val
                        ]
                    ];
            ?ie_4 [
                a premis:relationship;
                ?ie_5 [
                    a premis:relatedObjectIdentifier;
                    ?ie_6 [
                        a premis:relatedObjectIdentifierValue;
                        rdf:_1 ?ie_rel_rep_id
                    ]
                ]
            ]
            FILTER (?ie_type = "UUID")
            BIND(iri(concat(str(haObjId:), ?ie_val)) AS ?ie
        )
        BIND(iri(concat(str(haObjId:), ?ie_rel_rep_id)) AS  ?rep)
    }
    SERVICE <x-sparql-anything:location={{ bag_path }}/data/representations/representation_1/metadata/preservation/premis.xml> {
        {
            [] a premis:object;
                xsi:type "premis:representation";
                ?rep_1 [
                    a premis:objectIdentifier;
                    ?rep_2 [
                        a premis:objectIdentifierType;
                            rdf:_1 ?rep_type
                    ];
                    ?rep_3 [
                        a premis:objectIdentifierValue;
                            rdf:_1 ?rep_uuid
                    ]
                ];
                ?rep_4 [
                    a premis:relationship;
                    ?rep_5 [
                        a premis:relatedObjectIdentifier;
                        ?rep_6 [
                            a premis:relatedObjectIdentifierValue;
                            rdf:_1 ?rep_rel_ie_id
                        ]
                    ]
                ]
                BIND(iri(concat(str(haObjId:), ?rep_uuid)) AS ?rep)
                FILTER (?rep_type = "UUID")
        }

        {
            [] a premis:object;
                xsi:type "premis:file";
                ?file_1 [
                    a premis:objectIdentifier;
                    ?file_2 [
                        a premis:objectIdentifierType;
                            rdf:_1 ?file_type
                    ];
                    ?file_3 [
                        a premis:objectIdentifierValue;
                            rdf:_1 ?file_uuid
                    ]
                ];
                ?file_4 [
                    a premis:relationship;
                    ?file_5 [
                        a premis:relatedObjectIdentifier;
                        ?file_6 [
                            a premis:relatedObjectIdentifierValue;
                            rdf:_1 ?file_rel_rep_id
                        ]
                    ]
                ];
                ?file_7 [
                    a premis:objectCharacteristics;
                    ?file_8 [
                        a premis:fixity;
                        ?file_9 [
                            a premis:messageDigestAlgorithm;
                                xyz:valueURI ?fixity_uri;
                                # rdf:_1 ?file_digest_algo;
                        ];
                        ?file_10 [
                            a premis:messageDigest;
                            rdf:_1 ?file_digest
                        ]
                    ];
                    ?file_11 [
                        a premis:format;
                        ?file_12 [
                            a premis:formatRegistry;
                            ?file_13 [
                                a premis:formatRegistryKey;
                                rdf:_1 ?file_format
                            ]
                        ]
                    ];
                    ?file_14 [
                        a premis:size;
                        rdf:_1 ?file_size
                ]
                ];
                ?file_15 [
                    a premis:originalName;
                    rdf:_1 ?file_original_name
                ]
                BIND(iri(?fixity_uri) AS ?f_uri)
                BIND(iri(concat(str(haObjId:), ?file_uuid)) AS ?file)
                BIND(iri(concat(str(haObjId:), ?file_rel_rep_id)) AS ?rep)
        }
    }
    SERVICE <x-sparql-anything:location={{ bag_path }}/data/metadata/descriptive/dc_1.xml> {
        [] a fx:root;
            ?dc_1 [
                a dct:identifier;
                rdf:_1 ?id
            ];
            ?dc_2 ?dcterm .

        ?dcterm a ?dcterm_p;
           rdf:_1 ?dcterm_value .
        
        OPTIONAL {
            ?dcterm xsi:type ?dcterm_xsi_type.
        }

        BIND(
            IF(bound(?dcterm_xsi_type), STRDT(?dcterm_value, iri(?dcterm_xsi_type)), ?dcterm_value )
            AS ?dcterm_o)
    }
  }

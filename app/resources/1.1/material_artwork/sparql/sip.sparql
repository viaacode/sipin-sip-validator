PREFIX premis: <http://www.loc.gov/premis/v3#>
PREFIX premisowl: <http://www.loc.gov/premis/rdf/v3/>
PREFIX xsi: <http://www.w3.org/2001/XMLSchema-instance#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX haOrgId: <https://data.hetarchief.be/id/organization/>
PREFIX haObjId: <https://data.hetarchief.be/id/object/>
PREFIX haObj: <https://data.hetarchief.be/ns/object/>
PREFIX haSipId: <https://data.hetarchief.be/id/sip/1.1/>
PREFIX haSip: <https://data.hetarchief.be/ns/sip/>
PREFIX relSubType: <http://id.loc.gov/vocabulary/preservation/relationshipSubType/>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>
PREFIX cryptographicHashFunctions: <http://id.loc.gov/vocabulary/preservation/cryptographicHashFunctions/>
PREFIX edtf: <http://id.loc.gov/datatypes/edtf/>
PREFIX schema: <https://schema.org/>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX mets: <http://www.loc.gov/METS/>
PREFIX csip: <https://DILCIS.eu/XML/METS/CSIPExtensionMETS#>
PREFIX org: <http://www.w3.org/ns/org#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?sip a haSip:SIP;
    dct:conformsTo ?mets_profile;
    dct:conformsTo ?sip_profile;
    schema:agent [
        schema:roleName ?role;
        schema:agent ?agent
    ].

    ?agent  a ?atype;
            skos:prefLabel ?alabel;
            schema:name ?aname;
            schema:identifier ?agent_id
}
WHERE {
    SERVICE <x-sparql-anything:location={{ bag_path }}/data/mets.xml> {
        [
            a mets:mets;
            xyz:OBJID ?sip_id;
            xyz:PROFILE ?mets_profile;
            csip:CONTENTINFORMATIONTYPE ?sip_profile;
            ?1 [
                a mets:metsHdr;
                #csip:OAISPACKAGETYPE ?type;
                ?2 [
                    a mets:agent;
                    xyz:ROLE ?role;
                    xyz:TYPE ?agent_type;
                    ?3 [
                        a mets:name;
                        rdf:_1 ?agent_name
                    ];
                    ?4 [
                        a mets:note;
                        csip:NOTETYPE "IDENTIFICATIONCODE";
                        rdf:_1 ?agent_id
                    ]
                ]
            ]
        ] 

        BIND(iri(concat(str(haSipId:), ?sip_id)) AS ?sip)
        BIND(
            IF(?agent_type = "ORGANIZATION", org:Organization,
                IF(?agent_type = "INDIVIDUAL", schema:Person, ?null)
            ) AS ?atype)
        BIND(IF(?agent_type = "ORGANIZATION", STRLANG(?agent_name, "nl"), ?null2) AS ?alabel)
        BIND(IF(?agent_type = "INDIVIDUAL", STRDT(?agent_name, xsd:string), ?null3) AS ?aname)
        BIND(iri(concat(str(haOrgId:), ?agent_id)) AS  ?agent)
    }
}